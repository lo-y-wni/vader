# vader-import.cmake

include(CMakeFindDependencyMacro)

if( @jedicmake_FOUND@ AND NOT jedicmake_FOUND )
    find_dependency(jedicmake REQUIRED)
endif()

if(NOT Boost_FOUND)
    find_dependency(Boost REQUIRED)
endif()

if(NOT (NetCDF_C_FOUND AND NetCDF_Fortran_FOUND))
    find_dependency(NetCDF REQUIRED COMPONENTS C Fortran)
endif()

if(NOT eckit_FOUND)
    find_dependency(eckit REQUIRED)
endif()

if(NOT fckit_FOUND)
    find_dependency(fckit REQUIRED)
endif()

if(NOT oops_FOUND)
    find_dependency(oops REQUIRED)
endif()

# Optional dependencies

if(@gsw_FOUND@ AND NOT gsw_FOUND)
    find_dependency(gsw REQUIRED)
endif()

# Export Fortran compiler version for checking module compatibility
set(@PROJECT_NAME@_MODULES_Fortran_COMPILER_ID @CMAKE_Fortran_COMPILER_ID@)
set(@PROJECT_NAME@_MODULES_Fortran_COMPILER_VERSION @CMAKE_Fortran_COMPILER_VERSION@)
if(NOT @PROJECT_NAME@_MODULES_Fortran_COMPILER_ID STREQUAL CMAKE_Fortran_COMPILER_ID
   OR NOT @PROJECT_NAME@_MODULES_Fortran_COMPILER_VERSION VERSION_EQUAL CMAKE_Fortran_COMPILER_VERSION)
    message(SEND_ERROR "Package @PROJECT_NAME@ provides Fortran modules built with "
            "${@PROJECT_NAME@_MODULES_Fortran_COMPILER_ID}-${@PROJECT_NAME@_MODULES_Fortran_COMPILER_VERSION} "
            "but this build for ${PROJECT_NAME} uses incompatible compiler ${CMAKE_Fortran_COMPILER_ID}-${CMAKE_Fortran_COMPILER_VERSION}")
endif()
